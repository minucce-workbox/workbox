/*
Source:
	minucce


License:
	Code should be used only for educational, documentation and modding purposes.
	Please keep derivative work open source.
*/


.org 0x0a77
.area 0x0ad9 - 0x0a77



/*	HBlank = road drawing	*/


	push af
	push hl


	ldh a, (0x44)				; next LY line
	inc a

	ld h, 0xc8					; c8xx table
	ld l, a


	ld a, (hl)					; scx
	ldh (0x43), a
	inc h

	ld a, (hl)					; scy
	ldh (0x42), a
	inc h

	ld a, (hl)					; bgp
	ldh (0x47), a



; ##############################################



	ld a, l						; check HUD line
	cp a, 0x77-1
	jr nz, @@z__exit



	ld a, 0x01					; LCD = vblank only
	ldh (0xff), a


	ld hl, 0xcfa3



@@z__spin:
	ldh a, (0x41)				; wait h-blank
	and a, 0x02
	jr nz, @@z__spin


; =============================================


	ld a, 0x8b ^ 0x02			; lcdc = obj off
	ldh (0x40), a


	xor a						; scx
	ldh (0x43), a

	ld a, 0x89					; scy
	ldh (0x42), a

	ld a, (0xcfa5)				; bgp
	ldh (0x47), a


	call 0xff8a					; copy OAM sprites


	ldi a, (hl)					; obp1
	ldh (0x48), a

	ld a, (hl)					; obp2
	ldh (0x49), a


	ld a, 0x8b					; lcdc = obj on
	ldh (0x40), a


; ==============================================


	call 0x04d9
	call 0x04bf					; tone register
	call 0x043e					; sound



	ld a, (0xcfa6)				; sky scroll done
	or a
	jr z, @@z__exit


	call 0x0f41					; copy sky clouds tables


	ld a, (0xcfdd)
	ld (0xcfda), a

	xor a						; make next frame
	ld (0xcfa6), a


; =========================================================


@@z__exit:
	pop hl
	pop af

	reti



.endarea
; .notice int(0x0ad9 - orga())
