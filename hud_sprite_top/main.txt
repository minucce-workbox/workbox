/*
Source:
	minucce


License:
	Code should be used only for educational, documentation and modding purposes.
	Please keep derivative work open source.
*/


.org 0x0a77
.area 0x0ab7 - 0x0a77


/*	HBlank = road	*/


	push af
	push hl


	ldh a, (0x44)				; ly ?= HUD line  (77+)
	cp a, 0x77-1
	jr z, @z_0a93


	ld h, 0xc8					; c8xx table
	inc a
	ld l, a


	ld a, (hl)					; scx
	ldh (0x43), a
	inc h

	ld a, (hl)					; scy
	ldh (0x42), a
	inc h

	ld a, (hl)					; bgp
	ldh (0x47), a


	pop hl
	pop af
	reti


; =========================================


	nop


@z_0a93:
	ld a, 0x8b ^ 0x02			; lcdc = obj off
	ldh (0x40), a


	xor a						; scx
	ldh (0x43), a

	ld a, 0x89					; scy
	ldh (0x42), a

	ld a, (0xcfa5)				; bgp
	ldh (0x47), a


	call 0xff8a					; OAM dma


; ===============================================


	ld a, (0xcfa3)				; obp1
	ldh (0x48), a

	ld a, (0xcfa4)				; obp2
	ldh (0x49), a


	ld a, 0x8b					; lcdc = obj on
	ldh (0x40), a


	ld a, 0x01					; disable LCD interrupt
	ldh (0xff), a


.endarea
