/*
Source:
	minucce


License:
	Code should be used only for educational, documentation and modding purposes.
	Please keep derivative work open source.
*/


.org 0x1d3f
.area 0x1d7c - 0x1d3f


/*	Write tilemap to vram	*/

/*
4000 - 41e0
4620 - 4800
4c70 - 4e50
5310 - 54f0
5890 - 5a70
*/


	ld a, 0x04					; rom bank 04
	call 0x112a


	ld a, (0xce62)				; ptr table
	add a, a
	add a, a
	add a, 0x7c
	ld l, a

	ld a, 0x00					; carry warning - no xor
	adc a, 0x1d
	ld h, a

	ldi a, (hl)					; ptr
	ld e, a
	ldi a, (hl)
	ld d, a


	push hl
	ld hl, 0x9800				; vram = bg map  [0x1e0 bytes]



@@z_loop:
	call z_wait_blank

	ld a, (de)					; write map
	ldi (hl), a


	inc de
	bit 1, h					; upload 0x200
	jr z, @@z_loop


	pop hl


; ============================================


	ldi a, (hl)
	ld e, a
	ldi a, (hl)
	ld d, a

	ld hl, 0x9000				; mountains, hud  [0x800 bytes]



@@z_loop2:
	call z_wait_blank

	ld a, (de)					; write tile
	ldi (hl), a


	inc de
	bit 3, h
	jr z, @@z_loop2


; ================================================


	ld a, 0x01					; rom bank 01
	call 0x112a


	ret


.endarea
