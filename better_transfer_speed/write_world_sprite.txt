/*
Source:
	minucce


License:
	Code should be used only for educational, documentation and modding purposes.
	Please keep derivative work open source.
*/


.org 0x1d97
.area 0x1df9 - 0x1d97


	ld a, (0xce62)				; ptr table
	add a, a 
	add a, 0xf9
	ld l, a

	ld a, 0x00					; keep carry - no xor
	adc a, 0x1d
	ld h, a


; ================================================


	ld a, 0x02					; rom bank 02
	call 0x112a


	ldi a, (hl)					; off-road shoulder sprites
	ld e, a
	ld d, (hl)

	ld hl, 0x8bb0
	ld bc, 0x0240
	call @z_upload



	ld hl, 0x8df0				; on-road hazard sprites
	ld de, 0x7b0f
	ld bc, 0x0110
	call @z_upload


; ==============================================


	ld a, 0x06					; rom bank 06
	call 0x112a


	ld hl, 0x8fb0				; pause sprite
	ld de, 0x763e
	ld b, 0x28


@@z_loop:
	call z_wait_blank
	ld a, (de)					; byte 1
	ldi (hl), a


	call z_wait_blank
	xor a						; byte 2
	ldi (hl), a


	inc e
	inc e
	dec b
	jr nz, @@z_loop


; ==============================================


	ld a, 0x01					; rom bank 01
	call 0x112a


	ret


; <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<


@z_upload:
	inc b


@@z_loop:
	call z_wait_blank

	ld a, (de)					; write tile
	ldi (hl), a


	inc de
	dec c
	jr nz, @@z_loop


	dec b
	jr nz, @@z_loop


	ret


; <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<


/*	Wait until refresh period	*/


z_wait_blank:
	ldh a, (0x41)				; stat ?= active draw
	and a, 0x02
	jr nz, z_wait_blank

	ret


.endarea
