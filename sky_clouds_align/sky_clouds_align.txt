/*
Source:
	minucce


License:
	Code should be used only for educational, documentation and modding purposes.
	Please keep derivative work open source.
*/


.org 0x09c9
.area 0x0a46 - 0x09c9


/*	LYC (2) = sky 	*/


@z_sky_interrupt:
	push af
	push hl
	push de


	ld a, (0xcfda)				; clear sky  (00-19)
	cp a, 0x1a
	jr nc, @@z_09d8


	ld a, 0x83					; lcdc
	ldh (0x40), a

	ld a, (0xcfa1)				; bgp
	ldh (0x47), a

	jp @@z_land


; =================================================


@@z_09d8:
	ld a, (0xce6b)				; sky segment
	or a
	call z, @z_sky_init



	ld h, 0xcb					; cb00 table  [scy, lyc]
	ld l, a


	ldi a, (hl)					; <scy>
	ld d, a


	ld a, (hl) 					; <lyc>
	ld e, a
	dec a
	ldh (0x45), a


	ld a, l						; cb40 table  [16-bit scx]
	xor 0x40
	ld l, a


; =======================================


@@z_draw_sky_wait:
	ldh a, (0x41)				; stat = active draw
	and a, 0x02
	jr nz, @@z_draw_sky_wait



	ld a, (hl)					; scx
	ldh (0x43), a

	ld a, d						; scy

	inc e						; cp e, 0x00
	dec e
	jr z, @@z_mountain



	dec a						; scy
	ldh (0x42), a


	ld a, l						; next sky segment
	xor 0x40
	inc a
	ld (0xce6b), a


	jr @@z_lcd_exit


; =======================================


@@z_mountain:
	ldh (0x42), a				; scy



@@z_land:
	ld a, (0xcfda)				; lyc = horizon line
	ld (0xcfdc), a
	ldh (0x45), a


	ld hl, 0xd162				; horizon routine  (0a46)
	ld a, 0x0a
	ldd (hl), a
	ld (hl), 0x46


; =======================================


@@z_lcd_exit:
	ei							; check next line irq

	pop de
	pop hl
	pop af
	reti


; ========================================


/*	First line of sky data (LY = 0); still in VBlank routine.	*/


@z_sky_init:

/*	cb20 = cb00 memcpy buffer but sometimes timing too late  (scy - scx visual glitches)	*/

	ld h, 0xcb					; cb00, cb02 .. cb0e  [scy, lyc]
	ld l, 0x01



/*	0xff = skip sky segment  (sometimes several at start of cb00 table)	*/

@@z_sky_init_loop:
	ld a, (hl)					; <lyc data>
	inc a						; cp a, 0xff
	jr nz, @@z_sky_init_done


	inc l						; next sky segment
	inc l
	jr @@z_sky_init_loop



@@z_sky_init_done:

/* 	static sky values 	*/

	ld a, 0x83					; lcdc
	ldh (0x40), a

	ld a, (0xcfa1)				; bgp
	ldh (0x47), a



	dec l						; cache ptr
	ld a, l
	ld (0xce6b), a

	ret


.endarea
