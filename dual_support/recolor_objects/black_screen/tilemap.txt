/*
	Code should be used only for educational, documentation and modding purposes
*/


org 0x3dc7, 0x0


;
;  Write scrolling tilemap columns / rows
;


@tilemap_write:
	ldh a, (0x8c)
	add a, a

	add a, a						; bit 6 == 1
	ret nc

	add a, a						; bit 5 == 1
	ret nc


	ld hl, 0xcb00					; tilemap


; =========================================================
; =========================================================


@@loop:
	ldi a, (hl)						; vram high
	ld d, a

	or a							; 00 = stop
	ret z


	ldi a, (hl)						; vram low
	ld e, a


; =========================================================
; =========================================================


	ld a, (0xcaff)					; gbc only
	or a
	jr z, @@normal



	push hl							; map src
	push de							; vram dst


	ld bc, 0x0e20					; switch to color data
	add hl, bc


	ld a, 0xff						; vram bank 1
	ldh (0x4f), a					; vram bank


	call @@write_tilemap


	pop de							; vram dst
	pop hl							; src ptr


	xor a							; vram bank 0
	ldh (0x4f), a					; vram bank


; =========================================================
; =========================================================


@@normal:
	call @@write_tilemap


	jr @@loop



; /////////////////////////////////////////////////////////
; /////////////////////////////////////////////////////////



@@write_tilemap:
	ldi a, (hl)						; upper row
	ld (de), a
	inc e

	ldi a, (hl)
	ld (de), a


	ld a, e							; switch vram rows
	add a, 0x1f
	ld e, a


	ldi a, (hl)						; lower row
	ld (de), a
	inc e

	ldi a, (hl)
	ld (de), a


	ret


warnpc 0x3e13



; #########################################################
; #########################################################



;
;  Non-vblank tilemap writing
;


org 0x19b7, 0x0

	call load_room_tilemap			; screen_fade/copy.txt


	ldh a, (0x8c)					; allow normal uploads
	or a, 0x60
	ldh (0x8c), a

warnpc 0x1a06
